

Sender(i,inp,out,b) <= i(data).SenderSend<i,inp,out,data,b>;

SenderSend(i,inp,out,data,b) <= 'out<data, b>. inp(ackBit).
    case "b = ackBit"   : Sender<i,inp,out,"~b">
      [] "b = ~ackBit"  : SenderSend<i,inp,out,data,b>
      [] "ERR = ackBit" : SenderSend<i,inp,out,data,b> ;

Receiver(o, inp, out, b) <= inp(data, bit).
    case "b = bit"   : 'o<data>.'out<b>.Receiver<o,inp,out,"~b">
      [] "b = ~bit"  : 'out<"~bit"> . Receiver<o,inp,out,b>
      [] "ERR = bit" : 'out<"~b"> . Receiver<o,inp,out,b> ;

DataChan(inp,out) <= inp(data, bit).
    case True : 'out<data, bit> .DataChan<inp,out>
      [] True : 'out<ERR, ERR> .DataChan<inp,out> ;

ResponseChan(inp,out) <= inp(b).
    case True : 'out<b>    .ResponseChan<inp,out>
      [] True : 'out<"ERR">.ResponseChan<inp,out> ;

Buffer(i,o) <= i(x).'o<x>;

RecBuffer(i,o) <= i(x).'o<x>.RecBuffer<i,o>;

ABP(i,o,sb,rb) <= (new RcSn, SnRc) (
    Sender<i,RcSn,SnRc,sb> | Receiver<o,SnRc,RcSn,rb> );

ABP2(i,o,bitS,bitR) <= (new SnDc,DcRe,ReRc,RcSn)(
    Sender<i,RcSn,SnDc,bitS>   | DataChan<SnDc,DcRe> |
    Receiver<o,DcRe,ReRc,bitR> | ResponseChan<ReRc,RcSn> ) ;

